let go = //{./go};

\(:app, :appname, :basepath, :clientDeps, :endpoints, :fixPBPath, ...)
    let methodSig = \ep
        let {'name': (s: name), 'param': (a: [param]), 'stmt': (a: stmts), ...} = ep;
        let {'name': (s: pname), 'type': ptype, ...} = param;
        let rparam = $`${stmts
            filter .@item {{'ret': _, ...}: .}
            >> //seq.split(".", //seq.split(" ", .("ret")("payload").s)(2))(1)
        ::}`;
        $`${name}(ctx context.Context, ${pname} *pb.${go.leafType(ptype)}) (*pb.${rparam}, error)`;

    let pbPath = fixPBPath(//log.print(app('attrs')('source_path').@value.s));

    # TODO: sysl automapping between sysl types and protobuf types
    # TODO: use protobuf from sysl to use instead of precompiled from the api repository
    $`
        ${go.prelude(app, (clientDeps => $`${basepath}/${.import}`) | {(alias: 'pb', package: pbPath)})}

        // Service interface for ${appname}.
        type Service interface {
            ${(endpoints => \(@:_, @item: (@:_, @value: ep))
                let {'name': (s: name), 'param': (a: [{'name': (s: pname), ...}]), ...} = ep;
                $`
                    // ${name} ...
                    ${methodSig(ep)}
                `
            ) orderby .:::}
        }

        // Client implements a client for ${appname}.
        type Client struct {
            client pb.${appname}Client
            addr   string
        }

        // NewClient creates a new Client.
        func NewClient(addr string, connTimeout time.Duration) (*Client, error) {
            ctxWithTimeout, cancel := context.WithTimeout(context.Background(), connTimeout)
            defer cancel()

            conn, err := grpc.DialContext(ctxWithTimeout, addr, grpc.WithBlock())
            if err != nil {
                return nil, err
            }

            return &Client{pb.New${appname}Client(conn), addr}, nil
        }

        ${(endpoints => \(@:_, @item: (@:_, @value: ep))
            let {'name': (s: name), 'param': (a: [{'name': (s: pname), ...}]), ...} = ep;
            $`
                // ${name} ...
                func (s *Client) ${methodSig(ep)} {
                    return s.client.${name}(ctx, ${pname})
                }
            `
        ) orderby .:::}
    `
